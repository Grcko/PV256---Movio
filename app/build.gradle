def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    //standardní pojmenování apk souboru nic moc nevypovídá o daném buildu.
    //tímto způsobem snadno rozlišíte mezi jednotlivými buildy a verzemi
    applicationVariants.all { variant -> // .all stejně jako each prochází všechny prvky kolekce, ale aplikuje se i v případě, že v ní přibudou nové prvky
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "$project.name-$variant.name-$variant.versionName-${variant.versionCode}.apk")
        }
    }

    // nadefinujte si podepisování apk pro release build, ať jej můžete snadno spouštět. Jinak byste museli složitě pokaždé klikat na build->generate signed apk...
    //konstanty níže mějte uložené v gradle.properties
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    productFlavors {
        primary {
            resValue "string", "app_name", "Movio"
            buildConfigField "boolean", "logging", "false"
        }

        secondary {
            applicationId "cz.muni.fi.pv256.movio.uco396100.secondary"
            resValue "string", "app_name", "MovioSecondary"
            buildConfigField "boolean", "logging", "true"
         //   resValue "style", "app_theme", "@style/AppTheme" TODO
        }
    }

    defaultConfig {
        applicationId "cz.muni.fi.pv256.movio.uco396100"
        minSdkVersion 15 //minimální api 15 by mělo být dnes dostačující
        targetSdkVersion 22 //vždy mějte jako target poslední verzi SDK

        // nechejte resources jen pro vyjmenované konfigurace
        // takto snížíte velikost výsledného apk
        // nepotřebné resources budou odebrány i z externích knihoven
        resConfigs "cs", "en", "ja" //nechat resources jen pro vyjmenované jazyky
        resConfigs "hdpi", "xhdpi", "xxhdpi", "xxxhdpi" //v současnosti nemá smysl podporovat dpi nižší jako hdpi

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild

    }

    compileOptions { //chceme psát syntaxí javy 7
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {

        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            defaultConfig.versionName "${versionMajor}.${versionMinor}.${versionPatch}"
            defaultConfig.resConfigs "cs", "en"
        }

        // mějte release a debug build rozlišitelné. takto můžete nahrát na 1 zařízení 2 buildy aplikace
        // je vhodné také vizuálně odlišit ikonu debug verze
        debug {
            applicationIdSuffix '.debug'
            defaultConfig.versionName
            "${versionMajor}:${versionMinor}:${versionPatch}:${versionBuild}:${defaultConfig.versionCode}"
        }
    }

    lintOptions {
        textReport true // když je true, vygeneruje se textový report problémů (default je false)
        textOutput 'stdout' // kam zapsat výstup. buď do souboru - file nebo do 'stdout' (standardní výstup)
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.android.support:design:22.2.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'net.danlew:android.joda:2.9.0'
    compile 'com.google.code.gson:gson:2.4'
//    compile 'com.tonicartos:stickygridheaders:1.0.1'
}
